    1 REM : RANDOMIZE USR VAL "59453"
   10 GO SUB 60: GO TO 1000
   20 IF A<100 THEN LET A=100
   30 CLEAR ,A
   40 BORDER 0: PAPER 0: INK 7: GO SUB 100: LET HO=41: LET VE=23: LET SV=7: LET HG=256: LET VG=192
   50 GO SUB 60: RANDOMIZE : GO TO 1010
   60 DEF FN LEFT$(A$,N)=USR 58840: DEF FN MID$(A$,N,N)=USR 58843: DEF FN RIGHT$(A$,N)=USR 58846: DEF FN SHIFT$(A$)=USR 58855: DEF FN EOF(N)=USR 58858: DEF FN o()=USR VAL "59474": DEF FN o$()=USR VAL "59477": RETURN 
  100 CLS : PRINT AT 0,0;: RETURN 
  110 PRINT AT INT VE AND VE>=0 AND VE<24-PEEK 56806,INT HO AND HO>=0 AND HO<42;: RETURN 
  120 LET HO=43-PEEK 56802: LET VE=24-PEEK 56803: IF HO>41 THEN LET HO=0: LET VE=VE+1
  125 IF VE>23-PEEK 56806 THEN LET VE=23-PEEK 56806
  130 RETURN 
  150 PRINT "   ";SR$;"   ";: RETURN 
  200 LET IN=USR 56793
  201 LET IN$=CHR$ IN AND IN
  202 LET IN=IN-(32 AND IN>95 AND IN<127): RETURN 
  210 LET IN$=INKEY$#2: IF IN$="" THEN GO TO 210
  211 LET IN=CODE IN$: GO TO 202
  220 LET IN=CODE SCREEN$ (VE,HO): GO TO 202
  250 BEEP .25,21: RETURN 
  260 LET RV=RND: RETURN 
  270 LET FR=USR 59456: RETURN 
  280 POKE 65367,FR: RETURN 
  300 LET SR$=STR$ SR: RETURN 
  310 LET OS=ABS SR+.5*10^-CN: LET OI=INT OS: LET OD=OS-OI+1
  311 LET SR$=""
  312 IF OS>=1E8 THEN GO TO 321
  313 IF NOT CN THEN LET OD$="": GO TO 317
  315 LET OD$="."+FN MID$(STR$ OD,3,CN)
  316 IF LEN OD$<CN+1 THEN LET OD$=OD$+"0": GO TO 316
  317 LET SR$=STR$ OI+OD$
  318 IF SR<0 AND VAL SR$ THEN LET SR$="-"+SR$
  319 IF LEN SR$<CT THEN LET SR$=" "+SR$: GO TO 319
  320 IF LEN SR$>CT THEN LET SR$=""
  321 IF LEN SR$<CT THEN LET SR$="*"+SR$: GO TO 321
  322 RETURN 
  330 LET SR$=FN SHIFT$(SR$): RETURN 
  350 LPRINT SR$;: RETURN 
  360 LPRINT : RETURN 
  400 IF SV AND SD<105 THEN BEEP SD/10,SP-60: RETURN 
  401 LET OS=SD: LET OI=IN: LET OI$=IN$
  402 GO SUB 450: IF SD THEN GO TO 402
  403 LET SD=OS: LET IN=OI: LET IN$=OI$: RETURN 
  450 LET SD=(SD*5+USR 56796)/5: LET IN=PEEK 23560: GO TO 201
  500 POKE 65281+(NF+4)*4,0: LET IN=0: IF NF<2 THEN LET O=550 AND USR 59459: LET O=NF+4 AND USR (53950+3*NF): GO TO 543
  510 OPEN #FN o()+NF+4;"m";FN o()+1;FN o$()+NF$
  530 RETURN 
  540 LET O=550 AND USR 59459
  541 IF FN EOF(NF+4) THEN LET IN=1: LET IN$="": GO TO 543
  542 INPUT #NF+4;IN$: LET IN=FN EOF(NF+4)
  543 LET O=0 AND USR 59459: RETURN 
  550 PRINT #0;AT 1,0;"                                ";: LET IN=-1: LET IN$=""
  555 RETURN 
  560 PRINT #NF+4;SR$: LET IN=0: RETURN 
  580 LET IN=0: IF NF<2 THEN LET O=NF+4 AND USR 53956: RETURN 
  590 CLOSE #FN o()+NF+4
  595 RETURN 
  600 GO SUB 100: POKE 23678,191: RETURN 
  620 PLOT INVERSE NOT NOT CN;INT (256*(HO-INT HO)),191-INT (192*(VE-INT VE)): RETURN 
  630 DRAW INVERSE NOT NOT CN;INT (256*(HO-INT HO))-PEEK 23677,191-INT (192*(VE-INT VE))-PEEK 23678: RETURN 
  650 LET OH=INT (256*(HO-INT HO)): LET OV=191-INT (192*(VE-INT VE))
  660 POKE 23677,OH: POKE 23678,OV: INVERSE NOT NOT CN: LET O$=SR$ AND USR 56799: POKE 23677,OH: POKE 23678,OV: INVERSE 0: RETURN 
  950 POKE 65367,0: LET NF=0
  951 IF PEEK 23635+256*PEEK 23636-PEEK 23631-256*PEEK 23632<33 THEN GO TO 953
  952 GO SUB 580: LET NF=NF+1: IF NF<8 THEN GO TO 951
  953 CLEAR : STOP 
  960 CLS : LET a$="basicode-3"
  961 PRINT "CASSETTE OR MICRODRIVE/DISK ?";
  962 GO SUB 210
  963 IF IN=77 THEN GO TO 970
  964 IF IN<>67 THEN GO TO 962
  965 SAVE A$ LINE 966: POKE PEEK 23631+256*PEEK 23632+2,181: SAVE "CODE BASICODE 3"CODE 53950,11418: STOP 
  966 CLEAR VAL "53949": LOAD ""CODE : RANDOMIZE USR VAL "59450": RANDOMIZE USR 56790: GO TO 1000
  970 SAVE *"m";FN o()+1;FN o$()+A$ LINE FN o()+971: SAVE *"m";FN o()+1;"CODE BASICODE 3"CODE FN o()+53950,FN o()+11418: STOP 
  971 CLEAR VAL "53949": LOAD *"m";SGN PI;"CODE BASICODE 3"CODE : RANDOMIZE USR VAL "59450": RANDOMIZE USR 56790: GO TO 1000
  980 CLS : INPUT "PROGRAM NAME: ";A$: IF NOT LEN A$ OR LEN A$>10 THEN GO TO 980
  981 GO TO 961
 1000 BORDER 0: PAPER 0: INK 7: CLS : PRINT "         ** B A S I C O D E  3 **"
 1010 PRINT '''"  VERTAALPROGRAMMA / TRANSLATION PROGRAM"
 1020 PRINT ''"               VERSION 3.1"
 1030 PRINT '''"      VOOR MENU TOETS '*' & <ENTER>"
 1040 PRINT '''"            FOR MENU ENTER '*'"
 1050 PRINT '''"  (C) 1986, 87 JAN BREDENBEEK, HILVERSUM"
